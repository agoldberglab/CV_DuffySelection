initialize() {
	initializeTreeSeq();
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", s);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, 493103922);
	
	
	lines = readFile(ratesfile);
	ends = NULL;
	rates = NULL;
	
	for (line in lines)
	{
		components = strsplit(line, "\t");
		ends = c(ends, asInteger(components[0]));
		rates = c(rates, asFloat(components[1]));
	}
	
	initializeRecombinationRate(rates,ends);
}

1 late() {
	sim.addSubpop("p1", 1);
	sim.addSubpop("p2", 1);
	
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);
	
	p1.genomes.addNewDrawnMutation(m1, asInteger(159174683));
	
	sim.addSubpop("p3", 10000);
        m = asFloat(mig); 
        p3.setMigrationRates(c(p1, p2), c(m, 1-m));
}

2:20 {
	newSize = asInteger(round(1.05^(sim.generation-1) * 10000));
	p3.setSubpopulationSize(newSize);
}

2 late() {
	p3.setMigrationRates(c(p1, p2), c(0, 0));
	p1.setSubpopulationSize(0);
	p2.setSubpopulationSize(0);
}

20 late() {
	outname = "" + out + ".trees";
	sim.treeSeqOutput(outname);
	sim.simulationFinished();
}
